plugins {
  id 'java'
  id 'idea'
  id 'io.quarkus'
  id 'com.diffplug.spotless'
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  implementation 'io.quarkus:quarkus-arc'
  implementation 'io.quarkus:quarkus-vertx'
  implementation 'io.quarkus:quarkus-qute'

  implementation 'com.slack.api:bolt-socket-mode:1.23.1'
  implementation 'javax.websocket:javax.websocket-api:1.1'
  implementation 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.19'

  testImplementation 'io.quarkus:quarkus-junit5'
}

group 'com.lescastcodeurs'
version '1.0.0'

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

spotless {
  format 'misc', {
    target '**/*.md', '**/*.yml', '**/*.json', 'src/*/java/**/*.java'
    targetExclude 'src/main/resources/templates/**/*.*'

    prettier(['prettier': prettierVersion, 'prettier-plugin-java': prettierJavaVersion])
      .configFile('.prettierrc.yml')
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

// Auto-format code if not on CI
// See also https://docs.github.com/en/actions/learn-github-actions/environment-variables#default-environment-variables,
// https://docs.gitlab.com/ee/ci/variables/predefined_variables.html.
if (!System.getenv().containsKey('CI')) {
  tasks.named('spotlessCheck') {
    dependsOn(tasks.named('spotlessApply'))
  }
} // else execute only spotlessCheck
